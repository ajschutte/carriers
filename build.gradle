group 'com.andries'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

test {
    exclude "**/*IT.*"
}

task integrationTest(type: Test, description: "Runs the integration tests", group: "Verification") {
    include "**/*IT.*"
}

task deleteStagingDir(type: Delete) {
    description = "Deletes Carriers Staging Directory..."
    delete carmaStagingDir
}

task deleteDeployDir(type: Delete) {
    description = "Deletes Carriers Deploy Directory..."
    delete carmaDeployDir
}

clean {
    dependsOn deleteStagingDir
}

task explodedWar(type: Copy) {
    description = "Creates an Exploded Carriers, and Copies it to a Staging Directory..."
    into carmaStagingDir
    with war
}

war {
    dependsOn explodedWar
    archiveName 'carriers.war'
}

task deployLocal(type: Copy) {
    description = "Copies Exploded Carriers from Staging to a Deploy Directory..."
    dependsOn deleteDeployDir
    dependsOn explodedWar
    from carmaStagingDir
    into carmaDeployDir
}

dependencies {
    compile 'org.hibernate:hibernate-core:5.2.2.Final',
            'org.hibernate:hibernate-hikaricp:5.2.2.Final',
            'com.oracle:ojdbc7:12.1.0.1',
            'com.zaxxer:HikariCP:2.4.7',
            'org.springframework:spring-core:4.3.3.RELEASE',
            'org.springframework:spring-webmvc:4.3.3.RELEASE',
            'org.springframework:spring-aspects:4.3.3.RELEASE',
            'org.springframework:spring-orm:4.3.3.RELEASE',
            'org.springframework.data:spring-data-jpa:1.10.5.RELEASE',
            'com.fasterxml.jackson.core:jackson-databind:2.8.1',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.1',
            'org.slf4j:slf4j-log4j12:1.7.13',
            'com.rabbitmq:amqp-client:3.6.5',
            'com.mercurygate:mg-resource-apis:1.0-SNAPSHOT'
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    testCompile 'org.hamcrest:hamcrest-core:1.3',
            'junit:junit:4.12',
            'org.mockito:mockito-core:1.+',
            'org.springframework:spring-test:4.3.3.RELEASE',
            'org.glassfish.jersey.core:jersey-client:2.23.2',
            'org.glassfish.jersey.media:jersey-media-json-jackson:2.23.2'

}
